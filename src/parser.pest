WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "(*" ~ (!"*)" ~ ANY)* ~ "*)" }

toplevel = _{ SOI ~ (command)* ~ EOI }

command = _{ ( print | check | end_proof | define | whd | stop | proof | tac ) }
print = { "Print" ~ term ~ "." }
check = { "Check" ~ term ~ "." }
define = { ("def" | "Define" | "Definition") ~ name ~ otele ~ type_annot ~ def_body ~ "." }
whd = { "Whd" ~ term ~ "." }
debug = { "Set" ~ "Debug" ~ "\"" ~ string ~ "\"" }
stop = {  "Stop." }
proof = { "Proof" ~ "." }
end_proof = { qed | defined }
qed = { "Qed" ~ "." }
defined = { "Defined" ~ "." }

tac = { exact ~ "." | refine ~ "." | apply ~ "." | intro ~ "." | assumption ~ "."}
exact = { "exact" ~ term }
refine = { "refine" ~ term }
apply = { "apply" ~ term }
intro = { "intro" ~ names }
assumption = { "assumption" }

def_body = { ":=" ~ term | "" }
type_annot = { ":" ~ term | "" }

term = { sterm ~ ("->" ~ sterm)* }

otele = { (tele_item | name)* }
tele = { (tele_item | name)+ }
tele_item = { "(" ~ names ~ ":" ~ term ~ ")" }

sterm = { ( paren_term | fun | forall | ttype | tlet | name )+ }
paren_term = { "(" ~ term ~ ")" }
fun = { "fun" ~ tele ~ "=>" ~ term }
forall =  { "forall" ~ tele ~ "," ~ term }
ttype = { "Type" }
tlet = { "let" ~ name ~ ":" ~ term ~ ":=" ~ term ~ "in" ~ term }

keyword = { "in" }

name = @{ !(keyword ~ WHITESPACE) ~ ((ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")*) }
names = { name+ }

string = @{ (!"\"" ~ ANY)* }
