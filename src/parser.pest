WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "(*" ~ (!"*)" ~ ANY)* ~ "*)" }

toplevel = _{ SOI ~ (command)* ~ EOI }

command = _{ ( print | check | define | whd | stop ) }
print = { "Print" ~ term ~ "." }
check = { "Check" ~ term ~ "." }
define = { ("Define" | "Definition") ~ name ~ type_annot ~ ":=" ~ term ~ "." }
whd = { "Whd" ~ term ~ "." }
debug = { "Set" ~ "Debug" ~ "\"" ~ string ~ "\"" }
stop = {  "Stop." }

type_annot = { ":" ~ term | "" }

term = { sterm ~ ("->" ~ sterm)* }

tele = { tele_item* }
tele_item = { "(" ~ names ~ ":" ~ term ~ ")" }

sterm = { ( paren_term | fun | forall | ttype | tlet | name )+ }
paren_term = { "(" ~ term ~ ")" }
fun = { "fun" ~ tele ~ "=>" ~ term }
forall =  { "forall" ~ tele ~ "," ~ term }
ttype = { "Type" }
tlet = { "let" ~ name ~ ":" ~ term ~ ":=" ~ term ~ "in" ~ term }

keyword = { "in" }

name = @{ !(keyword ~ WHITESPACE) ~ ((ASCII_ALPHA | "_") ~ (ASCII_ALPHANUMERIC | "_")*) }
names = { name+ }

string = @{ (!"\"" ~ ANY)* }
